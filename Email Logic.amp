%%[

/* RETRIEVE FIELDS */
SET @fname = [FirstName]
SET @lname = [LastName]
SET @donorID = [DonorID]
SET @impactArea = [ImpactArea]

/* RETRIEVE PROJECT DATA */
SET @projectName = Lookup("Projects_Data", "ProjectName", "ImpactArea", @impactArea)
SET @projectStatus = Lookup("Projects_Data", "CompletionStatus", "ImpactArea", @impactArea)
SET @projectDesc = Lookup("Projects_Data", "ProjectDescription", "ImpactArea", @impactArea)
SET @projectCompDate = Lookup("Projects_Data", "CompletionDate", "ImpactArea", @impactArea)

SET @projectCompDateFormatted = FormatDate(@projectCompDate, "MMMM, YYYY")

/* FNAME AND LNAME CLEANUP */
IF EMPTY(@fname) THEN
  SET @finalName = "Valued Customer" 
  SET @subjectLine = "Thank You! Here’s How Your Support is Making an Impact"
ELSE
  SET @finalFirstName = Propercase(Trim(@fname)) 
  SET @subjectLine = CONCAT("Thank You, ", @finalFirstName, "! Here’s How Your Support is Making an Impact")
  IF EMPTY(@lname) THEN
    SET @finalName = @finalFirstName
  ELSE
    SET @finalLastName = Propercase(Trim(@lname))
    SET @finalName = CONCAT(@finalFirstName, " ", @finalLastName)
  ENDIF
ENDIF

/* LOGIC TO CALCULATE TOTAL DONATION AMOUNT */
SET @donationTotal = 0 /* INITIALIZE THE TOTAL DONATION AMOUNT TO ZERO */

SET @rows = LookupRows("Donations","DonorID", @donorID) /* RETRIEVE ALL ROWS FROM THE "DONATIONS" DATA EXTENSION WHERE THE DONOR ID MATCHES @donorID */
SET @rowCount = rowcount(@rows) /* GET THE NUMBER OF ROWS RETURNED FROM THE LOOKUP */

IF @rowCount > 0 THEN /* CHECK IF THERE ARE ANY DONATIONS FOUND FOR THE DONOR */

  for @i = 1 to @rowCount do /* LOOP THROUGH EACH ROW RETURNED */

    SET @row = row(@rows, @i) /* GET THE CURRENT ROW BASED ON THE LOOP COUNTER */
    SET @DonationAmount = field(@row,"DonationAmount") /* EXTRACT THE "DONATIONAMOUNT" VALUE FROM THE CURRENT ROW */
    SET @donationTotal = Add(@donationTotal, @DonationAmount) /* ADD THE CURRENT DONATION AMOUNT TO THE TOTAL */

  next @i 

ELSE
  RaiseError("No donations found") /* DISPLAY AN ERROR MESSAGE IF NO DONATIONS WERE FOUND */
ENDIF 

/* LOGIC FOR DYNAMIC DONATION COPY */
IF @donationTotal < 500 THEN
  SET @donationCopy = "We appreciate your support. Every bit of your contribution counts."
ELSEIF @donationTotal >= 500 AND @donationTotal < 1000 THEN
  SET @donationCopy = "As a token of our appreciation, we would like to invite you to an exclusive donor event. Click here to learn more."
ELSEIF @donationTotal >= 1000 AND @donationTotal < 5000 THEN
  SET @donationCopy = "We have a special thank-you video message from our CEO just for you. Watch it here."
ELSE
  SET @donationCopy = "We are honored to offer you a private tour of one of our conservation sites. Schedule your visit here."
ENDIF

/* FORMAT TOTAL DONATION AMOUNT*/
SET @totalContributions = FormatNumber(@donationTotal, "C")

]%%